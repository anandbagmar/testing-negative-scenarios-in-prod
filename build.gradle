import org.codehaus.groovy.runtime.GStringImpl

import java.text.SimpleDateFormat

buildscript {
    ext {
        gradleVersion = '8.10'
        teswizVersion = '229cf210e7'
    }
    repositories {
        mavenLocal()
    }
}

plugins {
    id "java"
    id "idea"
}
version '0.0.1'

repositories {
    flatDir {
        dirs 'libs'
    }
    mavenLocal()
    mavenCentral()
    maven {
        url = uri("https://repo.specmatic.io/releases")
    }
    maven {
        url = 'https://jitpack.io'
    }
}

compileJava {
    options.encoding = "UTF-8"
}

configurations {
    cucumberRuntime {
        extendsFrom testImplementation
    }
}

dependencies {
    implementation("com.github.znsio:teswiz:${teswizVersion}") {
        transitive = false
    }
}

static def getCurrentDatestamp() {
    Date today = new Date()
    SimpleDateFormat df = new SimpleDateFormat("dd-MMM")
    return df.format(today)
}

static def getMonth() {
    Date today = new Date()
    SimpleDateFormat df = new SimpleDateFormat("MMM-yyyy")
    return df.format(today)
}

static def getCurrentTimestamp() {
    Date today = new Date()
    SimpleDateFormat df = new SimpleDateFormat("HH-mm-ss")
    return df.format(today)
}

project.ext.LOG_DIR = "./reports/" + getMonth() + "/" + getCurrentDatestamp() + "/" + getCurrentTimestamp()
project.ext.LOG4J2_PROPERTIES_FILE = "${projectDir}/teswiz_configs/log4j2.properties"

test {
    doFirst {
        environment "APPLITOOLS_DONT_CLOSE_BATCHES", "true"
        environment "APPLITOOLS_LOG_DIR", "${project.LOG_DIR}/applitools_logs"
        environment "LOG_DIR", "${project.LOG_DIR}"
        System.setProperty "log4j.configurationFile", "${project.LOG4J2_PROPERTIES_FILE}"
    }
    useTestNG() {
        reports {
            html.required.set(true)
            junitXml.required.set(true)
            html.outputLocation.set(file("$project.LOG_DIR/html"))
            junitXml.outputLocation.set(file("$project.LOG_DIR/junit"))
        }
        reports.junitXml.outputPerTestCase(true)
        testLogging {
            events "started", "passed", "skipped", "failed"
            // show standard out and standard error of the test JVM(s) on the console
            showStandardStreams = true
            // show full exception trace
            exceptionFormat = 'full'
            showStackTraces = true
            showCauses = true
            showExceptions = true
        }
        println("Log directory: ${project.LOG_DIR}")
        println("Debug mode: " + System.getProperty('debug', 'false'))
        // attach debugger
        if (System.getProperty('debug', 'false') == 'true') {
            println("In debug mode")
            jvmArgs '-Xdebug', '-agentlib:jdwp=transport=dt_socket,server=y,suspend=y,' + 'address=*:5005'
        }
    }
}

def copyRpPropertiesIfMissing() {
    def rpFile = file('teswiz_configs/reportportal.properties')
    def rpTemplateFile = file('teswiz_configs/reportportal.properties.template')

    if (!rpFile.exists() && rpTemplateFile.exists()) {
        println "Copying $rpTemplateFile to $rpFile"
        rpFile.text = rpTemplateFile.text
    } else if (!rpTemplateFile.exists()) {
        println "$rpTemplateFile does not exist. Please create it."
    } else {
        println "$rpFile already exists."
    }
    System.setProperty("reportportal.properties.file", rpFile.absolutePath)
}

tasks.register('copyRpPropertiesIfMissing') {
    doLast {
        copyRpPropertiesIfMissing()
    }
}

gradle.taskGraph.whenReady { taskGraph ->
    if (taskGraph.hasTask(':build') || taskGraph.hasTask(':run')) {
        copyRpPropertiesIfMissing()
    }
}

tasks.register('run', JavaExec) {
    doFirst {
        println "Using LOG_DIR: ${project.LOG_DIR}"
        System.setProperty "LOG_DIR", "${project.LOG_DIR}"
        environment "APPLITOOLS_LOG_DIR", "${project.LOG_DIR}/applitools_logs"
        environment "APPLITOOLS_API_KEY", System.getenv("TESWIZ_APPLITOOLS_API_KEY")
        System.setProperty "log4j.configurationFile", "${project.LOG4J2_PROPERTIES_FILE}"

        def hostname = InetAddress.getLocalHost().getHostName()
        println "Hostname: $hostname"
        def ipAddress = InetAddress.getAllByName(hostname)
                .find { it.hostAddress.startsWith("192") || it.hostAddress.startsWith("172") || it.hostAddress.startsWith("10") }
                ?.hostAddress
        println "IP Address of this machine: $ipAddress"
        if (null == ipAddress) {
            println "Unable to get local IP address. NOT updating BASE_URL and REMOTE_WEBDRIVER_GRID_HOST_NAME"
        } else {
//            println "Updating BASE_URL ('http://$ipAddress:3000') and REMOTE_WEBDRIVER_GRID_HOST_NAME ('$ipAddress')"
//            environment "BASE_URL", "http://$ipAddress:3000"
            environment "REMOTE_WEBDRIVER_GRID_HOST_NAME", "$ipAddress"
        }

        def platform = System.getenv("PLATFORM")
        if (null == platform || platform.isEmpty()) {
            println("PLATFORM not provided, defaulting to 'web'")
            platform = "web"
        }

        def configFile = System.getenv("CONFIG")
        if (null == configFile || !file(configFile).exists()) {
            println("CONFIG file not provided, or does not exist")
            if (platform.equalsIgnoreCase("android")) {
                configFile = "./teswiz_configs/configs/jio_local_android_config.properties"
            } else {
                configFile = "./teswiz_configs/configs/jio_local_web_config.properties"
            }
            println("Using CONFIG file: ${configFile}")
        }
        assert file(configFile).exists()

        systemProperties = System.properties as Map<String, ?>
        def runnerArgs = [
                "${configFile}",
                "com/eot/e2e/steps",
                "./src/test/resources/com/eot/e2e/features"
        ]
        args = runnerArgs

        println("Debug mode: " + System.getProperty('debug', 'false'))
        // attach debugger
        // example: ./gradlew run -Ddebug=true
        if (System.getProperty('debug', 'false') == 'true') {
            println("In debug mode")
            jvmArgs '-Xdebug', '-agentlib:jdwp=transport=dt_socket,server=y,suspend=y,' +
                    'address=*:5005'
        }
    }
    mainClass = "com.znsio.teswiz.runner.Runner"
    classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
}

clean.doFirst {
    delete "${rootDir}/target"
    delete "${rootDir}/bin"
    delete "${rootDir}/build"
    delete "${rootDir}/out"
    println "Clean: Deleted bin, build, out, target and reports directories"
}

clean.doLast {
    mkdir "${rootDir}/reports"
    println "Created reports directory"
}

wrapper {
    gradleVersion = project.gradleVersion // version from gradle.properties
}
